为了在场景中使用多个光源，我们可以将光照计算封装到GLSL函数中。不然当光源很多的时候，我们的代码会变得非常复杂且难以理解。
GLSL的函数和C函数非常相似。有返回类型，参数，函数名。如果函数不是在main函数之前声明的，还必须在代码文件顶部声明一个原型。
当我们在场景中使用多光源时，我们需要有一个单独的颜色向量代表片段的输出颜色。对于每个光源将他们对总颜色的贡献加到片段输出的颜色向量中去。结合为一个最终的输出颜色。
## 定向光
结构体和处理函数定义
```Cpp
struct dirLight {
	vec3 direction;

	vec3 ambient;
	vec3 diffuse;
	vec3 specular;
};

vec3 CalcDirLight(dirLight light, vec3 normal, vec3 viewDir){
	//calculate ambient light
	vec3 ambientLight = light.ambient * vec3(texture(material.diffuse, TextureCoord));

	//calculate diffuse light
	vec3 lightDir = normalize(-light.direction);
	normal = normalize(normal);
	vec3 diffuseLight = max(dot(normal, lightDir), 0.0) * light.diffuse * vec3(texture(material.diffuse, TextureCoord));

	//calculate specular light
	vec3 reflectRay = reflect(-lightDir, normal);
	float spec = pow(max(dot(reflectRay, viewDir), 0.0), material.shininess);
	vec3 specularLight = spec * light.specular * vec3(texture(material.specular, TextureCoord));

	return ambientLight + diffuseLight + specularLight;
}
```
## 点光源
结构体和处理函数定义
```Cpp
struct pointLight {
	vec3 position;

	float linearFactor;
	float qudraFactor;

	vec3 ambient;
	vec3 diffuse;
	vec3 specular;
};

vec3 CalcPointLight(pointLight light, vec3 normal, vec3 fragPos, vec3 viewDir){
	//calculate ambient light
	vec3 ambientLight = light.ambient * vec3(texture(material.diffuse, TextureCoord));

    //calculate diffuse light
	vec3 lightDir = normalize(light.position - fragPos);
	normal = normalize(normal);
	vec3 diffuseLight = max(dot(normal, lightDir), 0.0) * light.diffuse * vec3(texture(material.diffuse, TextureCoord));

	//calculate specular light
	vec3 reflectRay = reflect(-lightDir, normal);
	float spec = pow(max(dot(reflectRay, viewDir), 0.0), material.shininess);
	vec3 specularLight = spec * light.specular * vec3(texture(material.specular, TextureCoord));

	//calculate decrase factor
	float distance = length(light.position - fragPos);
	float factor = 1.0 / (1.0 + light.linearFactor * distance + light.qudraFactor * light.qudraFactor * distance);

	return (ambientLight + diffuseLight + specularLight) * factor;
}
```
## 聚光
结构体和处理函数。
```Cpp
struct spotLight {
	vec3 position;

	vec3 towards;
	float cosPhi;
	float cosGamma;
	
	vec3 ambient;
	vec3 diffuse;
	vec3 specular;
};

vec3 CalcSpotLight(spotLight light, vec3 normal, vec3 fragPos, vec3 viewDir){
    //calculate ambient light
	vec3 ambicentLight =  light.ambient * vec3(texture(material.diffuse, TextureCoord));

	//calculate diffuse light
	vec3 lightDir = normalize(light.position - fragPos);
	normal = normalize(normal);
	vec3 diffuseLight = max(dot(lightDir,normal), 0.0) * vec3(texture(selfLight, TextureCoord)) * vec3(texture(material.diffuse, TextureCoord));

	//calculate specular light
	vec3 reflectRay = reflect(-lightDir, normal);
	float spec = pow(max(dot(viewDir, reflectRay), 0.0), material.shininess);
	vec3 specularLight = spec * light.specular * vec3(texture(material.specular, TextureCoord));

	float cosTheta =  dot(-lightDir, normalize(light.towards));
	float factor = clamp((cosTheta - light.cosGamma) / (light.cosPhi - light.cosGamma), 0.0, 1.0);

	diffuseLight *= factor;
	specularLight *= factor;

	return vec3(diffuseLight + ambicentLight + specularLight);
}
```
## Put them all together!
```Cpp
void main()
{
	vec3 result = vec3(0.0, 0.0, 0.0);
	
	result += CalcDirLight(dLight, Normal, viewVector);
	result += CalcSpotLight(sLight, Normal, fragPoint, viewVector);
	for(int i = 0; i < NR_POINT_LISHTS; i++){
		result += CalcPointLight(pLight[i], Normal, fragPoint, viewVector);
	}
	FragColor = vec4(result, 1.0);
}
```